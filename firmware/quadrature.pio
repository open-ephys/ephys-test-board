/* Copyright 2021 Adam Green (https://github.com/adamgreen/)

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

; Use the RP2040's PIO state machines to count quadrature encoder ticks.
.program quadrature_decoder

; Must start at 0 so that the following jump table can be jumped into with a
; 'mov pc, isr' instruction.
.origin 0
    ; 16 element jump table based on 4-bit encoder last state and current state.
    ;               previous-current
    jmp delta0      ; 00-00
    jmp plus1       ; 00-01
    jmp minus1      ; 00-10
    jmp delta0      ; 00-11
    jmp minus1      ; 01-00
    jmp delta0      ; 01-01
    jmp delta0      ; 01-10
    jmp plus1       ; 01-11
    jmp plus1       ; 10-00
    jmp delta0      ; 10-01
    jmp delta0      ; 10-10
    jmp minus1      ; 10-11
    jmp delta0      ; 11-00
    jmp minus1      ; 11-01
    jmp plus1       ; 11-10
    jmp delta0      ; 11-11

; Program actually starts here.
.wrap_target
delta0:
public start:
    mov isr, null       ; Make sure that the input shift register is cleared when table jumps to delta0.
    in y, 2             ; Upper 2-bits of address are formed from previous encoder pin readings
    mov y, pins         ; Lower 2-bits of address are formed from current encoder pin readings. Save in Y as well.
    in y, 2
    mov pc, isr         ; Jump into jump table which will then jump to delta0, minus1, or plus1 labels.
minus1:
    jmp x-- output      ; Decrement x
    jmp output
plus1:
    mov x, ~x           ; Increment x by calculating x=~(~x - 1)
    jmp x-- next2
next2:
    mov x, ~x
output:
    mov isr, x          ; Push out updated counter.
    push noblock
.wrap

% c-sdk {
#include "hardware/gpio.h"
#include "hardware/dma.h"
static inline void pio_quadrature_init(PIO pio, uint sm, uint enc_a, int *dma_chan, volatile uint32_t* counter, irq_handler_t dma_handler) {

    // Configure PIO
    pio_sm_config c = quadrature_decoder_program_get_default_config(0);
    sm_config_set_in_shift(&c, false, false, 32);
    sm_config_set_in_pins(&c, enc_a);
    sm_config_set_fifo_join(&c, PIO_FIFO_JOIN_RX);
    sm_config_set_clkdiv(&c, 255); // slow it down for human interaction to prevent glitching
    pio_sm_init(pio, sm, quadrature_decoder_offset_start, &c);

    // Configure DMA
    *dma_chan = dma_claim_unused_channel(true);
    dma_channel_config dc = dma_channel_get_default_config(*dma_chan);
    channel_config_set_transfer_data_size(&dc, DMA_SIZE_32);
    channel_config_set_read_increment(&dc, false);
    channel_config_set_write_increment(&dc, false);
    channel_config_set_dreq(&dc, pio_get_dreq(pio, sm, false));
    dma_channel_configure(
        *dma_chan,
        &dc,
        counter,           // Destination pointer
        &pio->rxf[sm],     // Source pointer
        1,                 // Trigger IRQ when count changes
        true               // Start immediately
    );

    // Tell the DMA to raise IRQ line 0 when the channel finishes a block
    dma_channel_set_irq0_enabled(*dma_chan, true);

    // Configure the processor to run dma_handler() when DMA IRQ 0 is asserted
    irq_set_exclusive_handler(DMA_IRQ_0, dma_handler);
    irq_set_enabled(DMA_IRQ_0, true);

    // Enable the PIO state machine
    pio_sm_set_enabled(pio, sm, true);
}
%}